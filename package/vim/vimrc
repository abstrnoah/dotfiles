set nocompatible
set exrc

"
" Setup vim-plug.
"
" Automatic installation.
" [https://github.com/junegunn/vim-plug/wiki/tips#automatic-installation]
if empty(glob('~/.vim/autoload/plug.vim'))
    silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
        \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
    autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin('~/.vimplug')
    " Must have.
    Plug '907th/vim-auto-save'
    Plug 'Townk/vim-autoclose'
    Plug 'tpope/vim-surround'
    Plug 'wellle/targets.vim'
    Plug 'ludovicchabant/vim-gutentags'
    Plug 'ctrlpvim/ctrlp.vim'
    Plug 'tpope/vim-repeat'
    " Should have.
    Plug 'justinmk/vim-sneak'
    Plug 'tpope/vim-eunuch'
    Plug 'dkarter/bullets.vim'
    Plug 'dhruvasagar/vim-table-mode'
    Plug 'tommcdo/vim-lion'
    Plug 'tpope/vim-commentary'
    " Fancy and useful extras.
    Plug 'preservim/nerdtree'
    Plug 'preservim/tagbar'
    Plug 'tpope/vim-abolish'
    " Frills.
    Plug 'Yggdroot/indentLine'
    " These ones are on thin ice.
    Plug 'vim-latex/vim-latex', {'for': 'tex'}
    Plug 'neoclide/coc.nvim', {'branch': 'release'}
    Plug 'honza/vim-snippets'
    Plug 'plasticboy/vim-markdown', {'for': 'markdown'}
    Plug 'puremourning/vimspector'
call plug#end()

" Called automatically by `plug#end`.
"filetype plugin indent on
"syntax enable


"
" Functions.
" TODO: Should probably be moved to its own plugin directory at some point.
"
function AN_NERDTree()
    " Call `:NERDTreeFind` if path nonempty, otherwise `:NERDTreeVCS`.
    if &modifiable && strlen(expand("%")) > 0 && !&diff
        NERDTreeFind
    else
        NERDTreeVCS
    endif
endfunction

" See [https://vim.fandom.com/wiki/Search_across_multiple_lines].
" TODO learn more about functions, esp. arguments.
" TODO also search across underscores.
" Search across lines and non-word characters. Case-sensitive if '!'.
function! SearchMultiline(bang, ...)
    if a:0 > 0
        "let sep = (a:bang) ? '\_W\+' : '\_s\+'
        let sep = '\(\_W\|_\)\+'
        let @/ = join(a:000, sep) . ((a:bang) ? "" : '\c')
    endif
endfunction

function EnableColorColumn()
    set colorcolumn=+1
endfunction

function ToggleColorColumn()
    if &colorcolumn == ""
        call EnableColorColumn()
    else
        setlocal colorcolumn=
    endif
endfunction

function ToggleTableMode()
    if tablemode#IsActive()
        call EnableColorColumn()
        call tablemode#Disable()
    else
        setlocal colorcolumn=
        call tablemode#Enable()
    endif
endfunction


"
" Mapping.
" For most mapping, see 'after/plugin/map.vim'. We put them there to ensure our
" mappings override both vim and plugin defaults. The convention is that all
" mappings go there unless they need to go elsewhere for some reason. Coc's
" bindings are in 'cocrc.vim' bc coc is so much. When plugins have an option
" like 'g:plugin_featureX_map', those are set here.
"
let mapleader = " "
set showcmd
set timeoutlen=200

"
" Commands.
"
command TrimTrailingWhitespace %s/\s\+$//g
command! -bang -nargs=* -complete=tag S
    \ call SearchMultiline(<bang>0, <f-args>)
    \ | normal! /<C-R>/<CR>
command! -bang -nargs=* -complete=tag SB
    \ call SearchMultiline(<bang>0, <f-args>)
    \ | normal! ?<C-R>/<CR>
command UnformatVisualYankReformat
    \ '<,'>s/\n\@<!\n\n\@!/ / | '<,'>yank + | normal! '<gq'>
"
" Ecoding.
"
set encoding=utf-8

"
" Graphical interface.
"
set relativenumber number
set scrolloff=3
set laststatus=2
set statusline=%<%f\ %h%m%r\ %y%=%-14.(%l,%c%V%)\ %P
" Folding really just gets in my way, so disable for now.
set nofoldenable
set foldmethod=syntax
set foldlevelstart=99

"
" Theme
"
" TODO This depends on my jenky gnome terminal colour scheme, need to overhaul
" colour pallet.
highlight StatusLine ctermbg=magenta ctermfg=black
set incsearch hlsearch
highlight Search ctermbg=black
highlight Visual ctermbg=black
highlight TrailingWhitespace ctermbg=red
match TrailingWhitespace /\s\+$/
autocmd BufWinEnter * match TrailingWhitespace /\s\+$/
" TODO Figure out how this pattern works.
autocmd InsertEnter * match TrailingWhitespace /\s\+\%#\@<!$/
autocmd InsertLeave * match TrailingWhitespace /\s\+$/
" Fixed preformance issue,
" see [https://vim.fandom.com/wiki/Highlight_unwanted_spaces].
autocmd BufWinLeave * call clearmatches()
" Accent current line number.
set cursorline
hi clear CursorLine
hi CursorLineNr ctermbg=brown ctermfg=black

"
" Indent, wrap, margins, format
"
call EnableColorColumn()
highlight ColorColumn ctermbg=black
set tabstop=4 expandtab softtabstop=4
set wrap linebreak nolist textwidth=80
set autoindent
set shiftwidth=4
set formatoptions+=j
set nojoinspaces

"
" Plugin configuration.
"

" autosave
let g:auto_save = 1
let g:auto_save_in_insert_mode = 0

" NERDTree
let NERDTreeQuitOnOpen = 1

" Coc
source ~/.vim/cocrc.vim

" Vimspector
let g:vimspector_install_gadgets = [
    \ 'debugpy',
    \ 'vscode-cpptools',
    \ 'vscode-java-debug'
\]

" CtrlP
let g:ctrlp_working_path_mode = "ra"
let g:ctrlp_show_hidden = 1
let g:ctrlp_user_command = {
    \ 'types': {
        \ 1: ['.git', 'cd %s && git ls-files -co --exclude-standard'],
    \ },
    \ 'fallback': 'find %s -type f'
\ }

" Gutentags
let g:gutentags_cache_dir = "~/.cache/gutentags"
set statusline^=%{gutentags#statusline()}\|

" Tagbar
let g:tagbar_sort = 1

" Bullet
let g:bullets_checkbox_markers = ' ○◐●✓'
" Use numbers for all levels of enumerated lists.
" This is partly for uniformity, but also because `vim-markdown` makes numbered
" lists pretty, yet does not recognize alphabetic lists so does not make them
" pretty.
let g:bullets_outline_levels = ['num']

" vim-markdown
let g:vim_markdown_override_foldtext = 0
let g:vim_markdown_toc_autofit = 1
" To avoid conflict with bullet plugin, still not ideal TODO.
let g:vim_markdown_auto_insert_bullets = 0
let g:vim_markdown_new_list_item_indent = 0

" cheat.sh-vim
let g:CheatDoNotReplaceKeywordPrg = 1

" indentLine
" Note that as it stands, indentLine seems to have control over conceal. So
" conceal color and behavior should be configured here, rather than via `set
" concealcursor`, etc.
let g:indentLine_color_term = "darkgray"
let g:indentLine_char = "┊"
let g:indentLine_concealcursor = ""

" vim-lion
let g:lion_squeeze_spaces = 1

" vim-sneak
let g:sneak#label = 1
